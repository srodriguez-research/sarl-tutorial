/** 
 * 
 */
package hello

import io.sarl.lang.core.Agent
import io.sarl.api.core.Lifecycle
import io.sarl.api.core.Behaviors
import io.sarl.api.core.Logging
import java.util.concurrent.atomic.AtomicLong
import io.sarl.api.core.Initialize

/** 
 * 
 */
event Factorial {
	var number : long
	var value : long
}

agent FactorialAgent {
	uses Lifecycle, Behaviors, Logging

	var upto = new AtomicLong(5l)
	
	on Initialize {
		
		wake(new Factorial => [it.number = 0l; it.value = 1l])
	}

	on Factorial [it.number < this.upto.get] {
		wake(new Factorial =>
			[it.number = occurrence.number + 1; it.value = occurrence.value * (occurrence.number + 1)])
	}

	on Factorial [occurrence.number == this.upto.get] {
		info("Factorial of " + this.upto.get + " is " + occurrence.value)
		killMe
	}

}
